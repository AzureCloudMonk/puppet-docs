---
layout: default
title: "ロールとプロファイル: 高度なプロファイルを設計する"
---

<p> <a href="./r_n_p_intro.html">ロールとプロファイルの概要</a>と<a href="./r_n_p_full_example.html">詳細な例</a>では、ロールとプロファイルの手法の仕組みについて説明しました。次に、より高度なプロファイルを記述していきます。
</p>

<p> <a href="./r_n_p_full_example.html">上から下に記述する例</a>で示したプロファイルは非常にシンプルで、複雑なサービスを管理する方法は示していませんでした。 この高度な例では、コード例を反復的にリファクタリングし、実際の懸念事項に対処します。 最終的には(若干の相違点はあるものの)Puppetの本稼働で使用するJenkinsプロファイルが作成されます。
</p>

<p> その中で、選択肢について説明するとともに、独自のプロファイルを設計するにあたって直面する一般的なトレードオフについても指摘します。
</p>

<h2 id="reminders">注意</h2>

<h3 id="the-initial-profile-code">初期のプロファイルコード</h3>

<p> 以前の例で作成したJenkinsプロファイルは以下のとおりです。
</p>

<pre><code class="language-puppet"># /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
class profile::jenkins::master (
  String $jenkins_port = '9091',
  String $java_dist    = 'jdk',
  String $java_version = 'latest',
) {

  class { 'jenkins':
    configure_firewall =&gt; true,
    install_java       =&gt; false,
    port               =&gt; $jenkins_port,
    config_hash        =&gt; {
      'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
      'JENKINS_PORT' =&gt; { 'value' =&gt; $jenkins_port },
    },
  }

  class { 'java':
    distribution =&gt; $java_dist,
    version      =&gt; $java_version,
    before       =&gt; Class['jenkins'],
  }
}
</code></pre>

<h3 id="the-rules-for-writing-profiles">プロファイル記述のルール</h3>

<p> プロファイルクラスを記述するためのルールは以下のとおりです。
</p>

<ol>
<li>
<a href="%7B%7Bpuppet%7D%7D/lang_classes.html#using-include"><code>include</code>関数</a>を使用して、プロファイルを安全に動作するようにします。プロファイルに<a href="%7B%7Bpuppet%7D%7D/lang_classes.html#include-like-vs-resource-like">リソースライクな宣言</a>を使用しないでください。</li>
  <li>プロファイルには、他のプロファイルを<code>include</code>できます。</li>
  <li>プロファイルには、コンポーネントクラスの<em>すべて</em>のクラスパラメータがあります。プロファイルでいずれかのクラスパラメータが省略されている場合、必ずデフォルト値が必要です。コンポーネントクラスはHieraデータの値を使用してはなりません。以前に省略されたクラスパラメータを設定する必要がある場合、プロファイルをリファクタリングします。</li>
  <li>コンポーネントクラスを設定するにあたり、プロファイルに必要な情報を入手するには3通りの方法があります。<ul>
<li>指定のパラメータに対して常に同じ値を使用する場合は、<strong>ハードコード化</strong>します。</li>
      <li>ハードコード化できない場合は、すでにわかっている情報をもとに<strong>計算</strong>します。</li>
      <li>最後に、計算できない場合は、データの中で <strong>ルックアップを実行</strong>します。ルックアップを減らすため、1つの質問に対する回答から複数のパラメータを得られるケースを特定します。</li>
    </ul>
</li>
</ol>
<h2 id="first-refactor-split-out-java">1回目のリファクタリング: Javaを分割する</h2>

<p> Jenkins master<em>および</em>Jenkins agentノードを管理します。agentプロファイルについては詳しく網羅しませんが、最初に直面する問題点は、いずれもJavaが必要であるということです。
</p>

<p> Javaクラス宣言をコピーし、貼り付けることができます。Javaクラス宣言は小さいため、複数のコピーを最新の状態に保つことは、それほど大きな負担ではありません。 今回は、Javaを個別のプロファイルに分割することにしました。 そうすると、管理を一度に行い、Javaプロファイルをagentプロファイルとmasterプロファイルの両方に含めることができます。
</p>

<blockquote>
  <p> <strong>注意</strong>: これは一般的なトレードオフです。 コード片を1か所にまとめておくと(「DRY(don't repeat yourself: 繰り返しを避ける)」原則と呼ばれます)、メンテナンスがしやすくなり、脆弱性を回避することができます。ただし、その代償として個々のプロファイルクラスが読みづらくなり、プロファイルの実際の動作を確認するには、より多くのファイルを確認する必要があります。
</p>

  <p> 読みやすさが損なわれないように、意味がある単位にコードを分割します。 このようにすると、Javaプロファイルのジョブはシンプルなので、名前から推測することができます。つまり、コードを読まなくても、Java 8を管理していることがわかります。 コメントも役に立ちます。
</p>
</blockquote>

<p> まず、JenkinsマシンでJavaをどれだけ構成するかを決定します。 過去の用途を見ると、2つのオプションしか使用していないことがわかります。そのうち1つはOracleのJava 8配布をインストールすること、もう1つはJenkinsモジュールが管理するOpenJDK 7をデフォルトで設定することです。 つまり、以下のことが可能です。
</p>

<ul>
<li>新しいJavaプロファイルはできるだけシンプルにする。Java 8をハードコード化し、設定は行わない。</li>
  <li>
<code>profile::jenkins::master</code>の2つのJavaパラメータを、1つのブールパラメータに置換する(JenkinsでJavaを処理するかどうか)。</li>
</ul>
<blockquote>
  <p> <strong>注意</strong>: これはルール4を実際に行ったものです。 複数の質問を1つに集約し、プロファイルの設定面を削減します。
</p>
</blockquote>

<p> 新しいパラメータリストは以下のとおりです。
</p>

<pre><code class="language-puppet">class profile::jenkins::master (
  String  $jenkins_port = '9091',
  Boolean $install_jenkins_java = true,
) { # ...
</code></pre>

<p> また、使用するJavaは以下のように選択します。
</p>

<pre><code class="language-puppet">  class { 'jenkins':
    configure_firewall =&gt; true,
    install_java       =&gt; $install_jenkins_java,    # &lt;--- here
    port               =&gt; $jenkins_port,
    config_hash        =&gt; {
      'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
      'JENKINS_PORT' =&gt; { 'value' =&gt; $jenkins_port },
    },
  }

  # When not using the jenkins module's java version, install java8.
  unless $install_jenkins_java  { include profile::jenkins::usage::java8 }
</code></pre>

<p> 新しいJavaプロファイルは以下のとおりです。
</p>

<pre><code class="language-puppet"># Class: profile::jenkins::usage::java8
# Sets up java8 for Jenkins on Debian
#
class profile::jenkins::usage::java8 {
  motd::register { 'Java usage profile (profile::jenkins::usage::java8)': }

  # OpenJDK 7 is already managed by the Jenkins module.
  # ::jenkins::install_java or ::jenkins::agent::install_java should be false to use this profile
  # this can be set through the class parameter $install_jenkins_java
  case $::osfamily {
    'debian': {
      class { 'java':
        distribution =&gt; 'oracle-jdk8',
        version      =&gt; '8u92',
      }

      package { 'tzdata-java':
        ensure =&gt; latest,
      }
    }
    default: {
      notify { "profile::jenkins::usage::java8 cannot set up JDK on ${::osfamily}": }
    }
  }
}
</code></pre>

<h3 id="diff-of-first-refactor">1回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -1,13 +1,12 @@
 # /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
 class profile::jenkins::master (
-  String $jenkins_port = '9091',
-  String $java_dist    = 'jdk',
-  String $java_version = 'latest',
+  String  $jenkins_port = '9091',
+  Boolean $install_jenkins_java = true,
 ) {

   class { 'jenkins':
     configure_firewall =&gt; true,
-    install_java       =&gt; false,
+    install_java       =&gt; $install_jenkins_java,
     port               =&gt; $jenkins_port,
     config_hash        =&gt; {
       'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
@@ -15,9 +14,6 @@ class profile::jenkins::master (
     },
   }

-  class { 'java':
-    distribution =&gt; $java_dist,
-    version      =&gt; $java_version,
-    before       =&gt; Class['jenkins'],
-  }
+  # When not using the jenkins module's java version, install java8.
+  unless $install_jenkins_java  { include profile::jenkins::usage::java8 }
 }
</code></pre>

<h2 id="second-refactor-manage-the-heap">2回目のリファクタリング: ヒープを管理する</h2>

<p> Puppetでは、Jenkinsアプリ用にJavaのヒープサイズを管理します。本稼働サーバーには十分なメモリが搭載されておらず、ヘビーユースには対応していませんでした。
</p>

<p> Jenkinsモジュールには、システムプロパティを管理するためのユーザ定義のタイプ<code>jenkins::sysconfig</code>があるため、それを使用します。
</p>

<pre><code class="language-puppet">  # Manage the heap size on the master, in MB.
  if($::memorysize_mb =~ Number and $::memorysize_mb &gt; 8192)
  {
    # anything over 8GB we should keep max 4GB for OS and others
    $heap = sprintf('%.0f', $::memorysize_mb - 4096)
  } else {
    # This is calculated as 50% of the total memory.
    $heap = sprintf('%.0f', $::memorysize_mb * 0.5)
  }
  # Set java params, like heap min and max sizes. See
  # https://wiki.jenkins-ci.org/display/JENKINS/Features+controlled+by+system+properties
  jenkins::sysconfig { 'JAVA_ARGS':
    value =&gt; "-Xms${heap}m -Xmx${heap}m -Djava.awt.headless=true -XX:+UseConcMarkSweepGC -XX:+CMSClassUnloadingEnabled -Dhudson.model.DirectoryBrowserSupport.CSP=\\\"default-src 'self'; img-src 'self'; style-src 'self';\\\"",
  }
</code></pre>

<blockquote>
  <p> <strong>注意</strong>: ここでもルール4が適用されます。余分なメモリを必要とする小さいJenkins masterがあり、ハードコード化できないからです。 ただし、本稼働のmasterは必ずパワフルなマシンにあるため、マシンのメモリサイズに基づいてヒープを計算できます(<a href="%7B%7Bpuppet%7D%7D/lang_facts_and_builtin_vars.html">fact</a>としてアクセス可能)。 これにより、余分な設定を行わずに済みます。
</p>
</blockquote>

<h3 id="diff-of-second-refactor">2回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -16,4 +16,20 @@ class profile::jenkins::master (

   # When not using the jenkins module's java version, install java8.
   unless $install_jenkins_java  { include profile::jenkins::usage::java8 }
+
+  # Manage the heap size on the master, in MB.
+  if($::memorysize_mb =~ Number and $::memorysize_mb &gt; 8192)
+  {
+    # anything over 8GB we should keep max 4GB for OS and others
+    $heap = sprintf('%.0f', $::memorysize_mb - 4096)
+  } else {
+    # This is calculated as 50% of the total memory.
+    $heap = sprintf('%.0f', $::memorysize_mb * 0.5)
+  }
+  # Set java params, like heap min and max sizes. See
+  # https://wiki.jenkins-ci.org/display/JENKINS/Features+controlled+by+system+properties
+  jenkins::sysconfig { 'JAVA_ARGS':
+    value =&gt; "-Xms${heap}m -Xmx${heap}m -Djava.awt.headless=true -XX:+UseConcMarkSweepGC -XX:+CMSClassUnloadingEnabled -Dhudson.model.DirectoryBrowserSupport.CSP=\\\"default-src 'self'; img-src 'self'; style-src 'self';\\\"",
+  }
+
 }
</code></pre>

<h2 id="third-refactor-pin-the-version">3回目のリファクタリング: バージョンを固定する</h2>

<p> 予期しないアップグレードを回避するため、Jenkinsを特定のバージョンに固定します。 そのためには、Jenkinsを内部パッケージリポジトリに追加するのではなく、直接パッケージURLを使用します。 組織によっては、やり方が異なることもあります。
</p>

<p> まず、アップグレードをコントロールするパラメータを追加します。 これで、<code>.../data/groups/ci.yaml</code>をそのままにしておきながら<code>.../data/groups/ci/dev.yaml</code>に新しい値を設定できるようになります。開発マシンは最初に新しいJenkinsバージョンを取得するため、本稼働マシンをアップグレードする前に、すべてを予期しているとおりに動作させることができます。
</p>

<pre><code class="language-puppet">class profile::jenkins::master (
  Variant[String[1], Boolean] $direct_download = 'http://pkg.jenkins-ci.org/debian-stable/binary/jenkins_1.642.2_all.deb',
  # ...
) { # ...
</code></pre>

<p> 次に、Jenkinsクラスに必須のパラメータを設定します。
</p>

<pre><code class="language-puppet">  class { 'jenkins':
    lts                =&gt; true,                    # &lt;-- here
    repo               =&gt; true,                    # &lt;-- here
    direct_download    =&gt; $direct_download,        # &lt;-- here
    version            =&gt; 'latest',                # &lt;-- here
    service_enable     =&gt; true,
    service_ensure     =&gt; running,
    configure_firewall =&gt; true,
    install_java       =&gt; $install_jenkins_java,
    port               =&gt; $jenkins_port,
    config_hash        =&gt; {
      'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
      'JENKINS_PORT' =&gt; { 'value' =&gt; $jenkins_port },
    },
  }
</code></pre>

<p> 今回は、Jenkins<em>サービス</em>の明示的な管理も追加しました。
</p>

<h3 id="diff-of-third-refactor">3回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -1,10 +1,17 @@
 # /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
 class profile::jenkins::master (
-  String  $jenkins_port = '9091',
-  Boolean $install_jenkins_java = true,
+  String                      $jenkins_port = '9091',
+  Variant[String[1], Boolean] $direct_download = 'http://pkg.jenkins-ci.org/debian-stable/binary/jenkins_1.642.2_all.deb',
+  Boolean                     $install_jenkins_java = true,
 ) {

   class { 'jenkins':
+    lts                =&gt; true,
+    repo               =&gt; true,
+    direct_download    =&gt; $direct_download,
+    version            =&gt; 'latest',
+    service_enable     =&gt; true,
+    service_ensure     =&gt; running,
     configure_firewall =&gt; true,
     install_java       =&gt; $install_jenkins_java,
     port               =&gt; $jenkins_port,
</code></pre>

<h2 id="fourth-refactor-manually-manage-the-user-account">4回目のリファクタリング: ユーザアカウントを手動で管理する</h2>

<p> インフラでは多数のユーザアカウントを管理するため、一元的に処理します。 <code>profile::server</code>クラスは<code>virtual::users</code>をプルします。ここには多数の<a href="%7B%7Bpuppet%7D%7D/lang_virtual.html">仮想リソース</a>が含まれており、ユーザが指定マシンにログインする場合は必要に応じて個別に提供できます。
</p>

<blockquote>
  <p> <strong>注意</strong>: これには代償が伴います。この処理は離れた場所から行われるため、特定のプロファイルに対してどのユーザを有効にするか確認する場合に、多くのファイルを読む必要があります。 ただし、ここでは代償を払う価値があると判断しました。すべてのユーザアカウントを記述したファイルは1つまたは2つしかないため、存在する可能性があるすべてのユーザを確認し、一貫して管理することは難しくないからです。
</p>

  <p> 今回の選択によって、(問題なく対処できる)ある部分については手間が増えますが、その代わりに別の部分(そこで問題に直面すると手に負えなくなる可能性がある)の問題を回避することができます。 このような判断を行うには、同僚の能力と、既存のコードベースおよびサポートされるサービスの限度を把握しておく必要があります。
</p>
</blockquote>

<p> そのため、この例では、Jenkinsプロファイルが同様に動作するように変更します。他のユーザアカウントに合わせて、<code>jenkins</code>ユーザアカウントを管理します。 同時に、Jenkinsのパッケージ化の方法によっては問題となり得るディレクトリも管理します。
</p>

<p> 必要な値の中には、Jenkins agentとJenkins masterの両方によって使用されるものもあるため、これらをparamsクラスに格納します。これは共有変数を設定するクラスで、リソースの管理は行いません。 ただし、これは負荷の大きいソリューションであるため、使用する前に実際の値が提供されるまで待機する必要があります。 今回のケースでは、OS固有のagentプロファイルが多数あるため(この例には含まれていません)、paramクラスの使用が効果的です。
</p>

<blockquote>
  <p> <strong>注意</strong>: 前回と同様、「don't repeat yourself(繰り返しを避ける)」と「keep it readable(読みやすくする)」は相反する原則であるため、適度なバランスを見出す必要があります。
</p>
</blockquote>

<pre><code class="language-puppet">  # We rely on virtual resources that are ultimately declared by profile::server.
  include profile::server

  # Some default values that vary by OS:
  include profile::jenkins::params
  $jenkins_owner          = $profile::jenkins::params::jenkins_owner
  $jenkins_group          = $profile::jenkins::params::jenkins_group
  $master_config_dir      = $profile::jenkins::params::master_config_dir

  file { '/var/run/jenkins': ensure =&gt; 'directory' }

  # Because our account::user class manages the '${master_config_dir}' directory
  # as the 'jenkins' user's homedir (as it should), we need to manage
  # `${master_config_dir}/plugins` here to prevent the upstream
  # rtyler-jenkins module from trying to manage the homedir as the config
  # dir. For more info, see the upstream module's `manifests/plugin.pp`
  # manifest.
  file { "${master_config_dir}/plugins":
    ensure  =&gt; directory,
    owner   =&gt; $jenkins_owner,
    group   =&gt; $jenkins_group,
    mode    =&gt; '0755',
    require =&gt; [Group[$jenkins_group], User[$jenkins_owner]],
  }

  Account::User &lt;| tag == 'jenkins' |&gt;

  class { 'jenkins':
    lts                =&gt; true,
    repo               =&gt; true,
    direct_download    =&gt; $direct_download,
    version            =&gt; 'latest',
    service_enable     =&gt; true,
    service_ensure     =&gt; running,
    configure_firewall =&gt; true,
    install_java       =&gt; $install_jenkins_java,
    manage_user        =&gt; false,                    # &lt;-- here
    manage_group       =&gt; false,                    # &lt;-- here
    manage_datadirs    =&gt; false,                    # &lt;-- here
    port               =&gt; $jenkins_port,
    config_hash        =&gt; {
      'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
      'JENKINS_PORT' =&gt; { 'value' =&gt; $jenkins_port },
    },
  }
</code></pre>

<p> 上記のコードでは、以下の3点に注意してください。
</p>

<ul>
<li>ユーザは、自作のユーザ定義タイプ<code>account::user</code>で管理します。ここで<code>user</code>リソースの他にもいくつかの宣言を行います。</li>
  <li>
<code>Account::User</code><a href="%7B%7Bpuppet%7D%7D/lang_collectors.html">リソースコレクタ</a>を使用してJenkinsユーザを作成します。これを行うには、<code>profile::server</code>を宣言する必要があります。</li>
  <li>Jenkinsクラスの<code>manage_user</code>、<code>manage_group</code>、<code>manage_datadirs</code>パラメータをfalseに設定します。</li>
  <li>これで、<code>plugins</code>ディレクトリと<code>run</code>ディレクトリが明示的に管理されます。</li>
</ul>
<h3 id="diff-of-fourth-refactor">4回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -5,6 +5,33 @@ class profile::jenkins::master (
   Boolean                     $install_jenkins_java = true,
 ) {

+  # We rely on virtual resources that are ultimately declared by profile::server.
+  include profile::server
+
+  # Some default values that vary by OS:
+  include profile::jenkins::params
+  $jenkins_owner          = $profile::jenkins::params::jenkins_owner
+  $jenkins_group          = $profile::jenkins::params::jenkins_group
+  $master_config_dir      = $profile::jenkins::params::master_config_dir
+
+  file { '/var/run/jenkins': ensure =&gt; 'directory' }
+
+  # Because our account::user class manages the '${master_config_dir}' directory
+  # as the 'jenkins' user's homedir (as it should), we need to manage
+  # `${master_config_dir}/plugins` here to prevent the upstream
+  # rtyler-jenkins module from trying to manage the homedir as the config
+  # dir. For more info, see the upstream module's `manifests/plugin.pp`
+  # manifest.
+  file { "${master_config_dir}/plugins":
+    ensure  =&gt; directory,
+    owner   =&gt; $jenkins_owner,
+    group   =&gt; $jenkins_group,
+    mode    =&gt; '0755',
+    require =&gt; [Group[$jenkins_group], User[$jenkins_owner]],
+  }
+
+  Account::User &lt;| tag == 'jenkins' |&gt;
+
   class { 'jenkins':
     lts                =&gt; true,
     repo               =&gt; true,
@@ -14,6 +41,9 @@ class profile::jenkins::master (
     service_ensure     =&gt; running,
     configure_firewall =&gt; true,
     install_java       =&gt; $install_jenkins_java,
+    manage_user        =&gt; false,
+    manage_group       =&gt; false,
+    manage_datadirs    =&gt; false,
     port               =&gt; $jenkins_port,
     config_hash        =&gt; {
       'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
</code></pre>

<h2 id="fifth-refactor-manage-more-dependencies">5回目のリファクタリング: その他の依存関係を管理する</h2>

<p> Jenkinsでは、Gitを必ずインストールする必要があります(PuppetのソースコントロールではGitが使用されるため)。また、非公開のGitリポジトリにアクセスし、Jenkins agentノードでコマンドを実行するには、SSHキーが必要です。Jenkinsプラグインの標準的なリストもあるため、これらも管理します。
</p>

<p> Gitの管理は難しくありません。
</p>

<pre><code class="language-puppet">  package { 'git':
    ensure =&gt; present,
  }
</code></pre>

<p> SSHキーは機密性の高いコンテンツなので、管理はやや困難です。 SSHキーは、他のPuppetコードとともにバージョン管理にチェックインできないため、特定のPuppetサーバーの<a href="%7B%7Bpuppet%7D%7D/file_serving.html">カスタムマウントポイント</a>に格納します。
</p>

<p> このサーバーは通常のPuppetサーバーとは異なるため、アクセスのためのルールを定めます。ホスト名をハードコード化する代わりに、データからルックアップする必要があります。 これにより、セキュアサーバーが移動しても1か所で変更するだけで対応できます。
</p>

<pre><code class="language-puppet">  $secure_server = lookup('puppetlabs::ssl::secure_server')

  file { "${master_config_dir}/.ssh":
    ensure =&gt; directory,
    owner  =&gt; $jenkins_owner,
    group  =&gt; $jenkins_group,
    mode   =&gt; '0700',
  }

  file { "${master_config_dir}/.ssh/id_rsa":
    ensure =&gt; file,
    owner  =&gt; $jenkins_owner,
    group  =&gt; $jenkins_group,
    mode   =&gt; '0600',
    source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins",
  }

  file { "${master_config_dir}/.ssh/id_rsa.pub":
    ensure =&gt; file,
    owner  =&gt; $jenkins_owner,
    group  =&gt; $jenkins_group,
    mode   =&gt; '0640',
    source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins.pub",
  }
</code></pre>

<p> プラグインもやや慎重に検討する必要があります。プラグインを手動で設定するJenkins masterもいくつか存在するからです。 そのため、ベースリストを別個のプロファイルに格納し、それを使用するかどうかはパラメータでコントロールします。
</p>

<pre><code class="language-puppet">class profile::jenkins::master (
  Boolean                     $manage_plugins = false,
  # ...
) {
  # ...
  if $manage_plugins {
    include profile::jenkins::master::plugins
  }
</code></pre>

<p> プラグインプロファイルでは、Jenkinsモジュールが提供する<code>jenkins::plugin</code>リソース型を使用できます。
</p>

<pre><code class="language-puppet"># /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master/plugins.pp
class profile::jenkins::master::plugins {
  jenkins::plugin { 'audit2db':          }
  jenkins::plugin { 'credentials':       }
  jenkins::plugin { 'jquery':            }
  jenkins::plugin { 'job-import-plugin': }
  jenkins::plugin { 'ldap':              }
  jenkins::plugin { 'mailer':            }
  jenkins::plugin { 'metadata':          }
  # ... and so on.
}
</code></pre>

<h3 id="diff-of-fifth-refactor">5回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -1,6 +1,7 @@
 # /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
 class profile::jenkins::master (
   String                      $jenkins_port = '9091',
+  Boolean                     $manage_plugins = false,
   Variant[String[1], Boolean] $direct_download = 'http://pkg.jenkins-ci.org/debian-stable/binary/jenkins_1.642.2_all.deb',
   Boolean                     $install_jenkins_java = true,
 ) {
@@ -14,6 +15,20 @@ class profile::jenkins::master (
   $jenkins_group          = $profile::jenkins::params::jenkins_group
   $master_config_dir      = $profile::jenkins::params::master_config_dir

+  if $manage_plugins {
+    # About 40 jenkins::plugin resources:
+    include profile::jenkins::master::plugins
+  }
+
+  # Sensitive info (like SSH keys) isn't checked into version control like the
+  # rest of our modules; instead, it's served from a custom mount point on a
+  # designated server.
+  $secure_server = lookup('puppetlabs::ssl::secure_server')
+
+  package { 'git':
+    ensure =&gt; present,
+  }
+
   file { '/var/run/jenkins': ensure =&gt; 'directory' }

   # Because our account::user class manages the '${master_config_dir}' directory
@@ -69,4 +84,29 @@ class profile::jenkins::master (
     value =&gt; "-Xms${heap}m -Xmx${heap}m -Djava.awt.headless=true -XX:+UseConcMarkSweepGC -XX:+CMSClassUnloadingEnabled -Dhudson.model.DirectoryBrowserSupport.CSP=\\\"default-src 'self'; img-src 'self'; style-src 'self';\\\"",
   }

+  # Deploy the SSH keys that Jenkins needs to manage its agent machines and
+  # access Git repos.
+  file { "${master_config_dir}/.ssh":
+    ensure =&gt; directory,
+    owner  =&gt; $jenkins_owner,
+    group  =&gt; $jenkins_group,
+    mode   =&gt; '0700',
+  }
+
+  file { "${master_config_dir}/.ssh/id_rsa":
+    ensure =&gt; file,
+    owner  =&gt; $jenkins_owner,
+    group  =&gt; $jenkins_group,
+    mode   =&gt; '0600',
+    source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins",
+  }
+
+  file { "${master_config_dir}/.ssh/id_rsa.pub":
+    ensure =&gt; file,
+    owner  =&gt; $jenkins_owner,
+    group  =&gt; $jenkins_group,
+    mode   =&gt; '0640',
+    source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins.pub",
+  }
+
 }
</code></pre>

<h2 id="sixth-refactor-manage-logging-and-backups">6回目のリファクタリング: ロギングとバックアップを管理する</h2>

<p> ほとんどの場合は、バックアップを行うことをお勧めします。
</p>

<p> 自作の<code>backup</code>モジュールを使用することもできます。このモジュールは、<code>backup::job</code>リソース型を提供します(この前提条件については、<code>profile::server</code>が対処します)。 ただし、ここではバックアップをオプションにします。そうすることで、テスト目的で1回限りのJenkinsインスタンスを設定している場合に、バックアップサーバーに不要なものを誤って送信しないようにします。
</p>

<pre><code class="language-puppet">class profile::jenkins::master (
  Boolean                     $backups_enabled = false,
  # ...
) {
  # ...
  if $backups_enabled {
    backup::job { "jenkins-data-${::hostname}":
      files =&gt; $master_config_dir,
    }
  }
}
</code></pre>

<p> また、チームからは、Jenkinsログに対して競合するリクエストがありました。
</p>

<ul>
<li>他の大半のサービスと同じように、syslogを使いたいという人もいます。</li>
  <li>また、syslogにスパムが送信されないよう、個別のログファイルを使用し、ファイルをデフォルトよりも短期間で回転させたいという人もいます。</li>
</ul>
<p> この場合は新たなパラメータが必要となります。 ここで、<code>$jenkins_logs_to_syslog</code>というパラメータを作成し、<code>undef</code>をデフォルトにします。 これを標準的なsyslogファシリティ(<code>daemon.info</code>など)に設定すると、Jenkinsは独自のファイルではなく、そのファシリティにログを格納します。
</p>

<p> <code>jenkins::sysconfig</code>と自作の<code>logrotate::job</code>で処理を行います。
</p>

<pre><code class="language-puppet">class profile::jenkins::master (
  Optional[String[1]]         $jenkins_logs_to_syslog = undef,
  # ...
) {
  # ...
  if $jenkins_logs_to_syslog {
    jenkins::sysconfig { 'JENKINS_LOG':
      value =&gt; "$jenkins_logs_to_syslog",
    }
  }
  # ...
  logrotate::job { 'jenkins':
    log     =&gt; '/var/log/jenkins/jenkins.log',
    options =&gt; [
      'daily',
      'copytruncate',
      'missingok',
      'rotate 7',
      'compress',
      'delaycompress',
      'notifempty'
    ],
  }
}
</code></pre>

<h3 id="diff-of-sixth-refactor">6回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -1,8 +1,10 @@
 # /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
 class profile::jenkins::master (
   String                      $jenkins_port = '9091',
+  Boolean                     $backups_enabled = false,
   Boolean                     $manage_plugins = false,
   Variant[String[1], Boolean] $direct_download = 'http://pkg.jenkins-ci.org/debian-stable/binary/jenkins_1.642.2_all.deb',
+  Optional[String[1]]         $jenkins_logs_to_syslog = undef,
   Boolean                     $install_jenkins_java = true,
 ) {

@@ -84,6 +86,15 @@ class profile::jenkins::master (
     value =&gt; "-Xms${heap}m -Xmx${heap}m -Djava.awt.headless=true -XX:+UseConcMarkSweepGC -XX:+CMSClassUnloadingEnabled -Dhudson.model.DirectoryBrowserSupport.CSP=\\\"default-src 'self'; img-src 'self'; style-src 'self';\\\"",
   }

+  # Forward jenkins master logs to syslog.
+  # When set to facility.level the jenkins_log will use that value instead of a
+  # separate log file eg. daemon.info
+  if $jenkins_logs_to_syslog {
+    jenkins::sysconfig { 'JENKINS_LOG':
+      value =&gt; "$jenkins_logs_to_syslog",
+    }
+  }
+
   # Deploy the SSH keys that Jenkins needs to manage its agent machines and
   # access Git repos.
   file { "${master_config_dir}/.ssh":
@@ -109,4 +120,29 @@ class profile::jenkins::master (
     source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins.pub",
   }

+  # Back up Jenkins' data.
+  if $backups_enabled {
+    backup::job { "jenkins-data-${::hostname}":
+      files =&gt; $master_config_dir,
+    }
+  }
+
+  # (QENG-1829) Logrotate rules:
+  # Jenkins' default logrotate config retains too much data: by default, it
+  # rotates jenkins.log weekly and retains the last 52 weeks of logs.
+  # Considering we almost never look at the logs, let's rotate them daily
+  # and discard after 7 days to reduce disk usage.
+  logrotate::job { 'jenkins':
+    log     =&gt; '/var/log/jenkins/jenkins.log',
+    options =&gt; [
+      'daily',
+      'copytruncate',
+      'missingok',
+      'rotate 7',
+      'compress',
+      'delaycompress',
+      'notifempty'
+    ],
+  }
+
 }
</code></pre>

<h2 id="seventh-refactor-use-a-reverse-proxy-for-https">7回目のリファクタリング: HTTPSのリバースプロキシを使用する</h2>

<p> Jenkins WebインターフェースでHTTPSを使用してもらうため、Nginxリバースプロキシを実現します。 また、ポートを標準化します。Jenkinsアプリはデフォルトポートに常にバインドされ、プロキシではHTTPSは常にポート443を、HTTPは常にポート80を使用します。
</p>

<p> 通常のHTTPを使用可能な状態にしておく場合は、<code>$ssl</code>パラメータを提供します。 <code>false</code>に設定されている場合(デフォルト)、HTTPとHTTPSの両方を経由してJenkinsにアクセスできます。 <code>$site_alias</code>パラメータも追加します。こうすると、プロキシはノードのメインFQDN以外のホスト名もリッスンできます。
</p>

<pre><code class="language-puppet">class profile::jenkins::master (
  Boolean                     $ssl = false,
  Optional[String[1]]         $site_alias = undef,
  # IMPORTANT: notice that $jenkins_port is removed.
  # ...
</code></pre>

<p> Jenkinsクラスで<code>configure_firewall =&gt; false</code>を設定します。
</p>

<pre><code class="language-puppet">  class { 'jenkins':
    lts                =&gt; true,
    repo               =&gt; true,
    direct_download    =&gt; $direct_download,
    version            =&gt; 'latest',
    service_enable     =&gt; true,
    service_ensure     =&gt; running,
    configure_firewall =&gt; false,                # &lt;-- here
    install_java       =&gt; $install_jenkins_java,
    manage_user        =&gt; false,
    manage_group       =&gt; false,
    manage_datadirs    =&gt; false,
    # IMPORTANT: notice that port and config_hash are removed.
  }
</code></pre>

<p> Nginxが到達できる場所にSSL証明書をデプロイする必要があります。 HTTPS上ではさまざまなサービスを提供しているため、そのプロファイルがすでに存在します。
</p>

<pre><code class="language-puppet">  # Deploy the SSL certificate/chain/key for sites on this domain.
  include profile::ssl::delivery_wildcard
</code></pre>

<p> ここで、その日のメッセージに情報を追加するのも良いでしょう(<a href="https://forge.puppet.com/puppetlabs/motd">puppetlabs/motd</a>が処理します)。
</p>

<pre><code class="language-puppet">  motd::register { 'Jenkins CI master (profile::jenkins::master)': }

  if $site_alias {
    motd::register { 'jenkins-site-alias':
      content =&gt; @("END"),
                 profile::jenkins::master::proxy

                 Jenkins site alias: ${site_alias}
                 |-END
      order   =&gt; 25,
    }
  }
</code></pre>

<p> 作業の大半は、<code>profile::jenkins::master::proxy</code>という名前の新規プロファイルによって処理されます。 簡略化するためコードは省略しますが、簡単に言うと以下の処理が行われます。
</p>

<ul>
<li>
<code>profile::nginx</code>を含める。</li>
  <li>
<a href="https://forge.puppet.com/jfryman/nginx">jfryman/nginx</a>のリソース型を使用してvhostを設定し、通常のHTTPを有効にしていない場合はHTTPSへのリダイレクトを強制する。</li>
  <li>アクセスおよびエラーログ用に、logstash転送を設定する。</li>
  <li>必要に応じて<code>profile::fw::https</code>を含めてファイアウォールルールを管理する。</li>
</ul>
<p> 次に、そのプロファイルをメインプロファイルで宣言します。
</p>

<pre><code class="language-puppet">  class { 'profile::jenkins::master::proxy':
    site_alias  =&gt; $site_alias,
    require_ssl =&gt; $ssl,
  }
</code></pre>

<blockquote>
  <p> <strong>重要</strong>: ここで、ロールとプロファイル手法のルールの中でも特に重要な、ルール1が破られています。 なぜ?
</p>

  <p> <code>profile::jenkins::master::proxy</code>は<code>profile::jenkins::master</code>に<strong>のみ</strong>属する「非公開」のプロファイルであるからです。 他のロールやプロファイルによって宣言されることはありません。
</p>

  <p> <em>読みやすさを維持する目的でのみ</em>コードを分離する場合は、これがルール1への唯一の例外になります。つまり、非公開プロファイルのコンテンツをメインプロファイルに貼り付けてまったく同じ効果を得られるのであれば、リソースライクの宣言を非公開プロファイルで使用することができます。 これにより、データルックアップをまとめ、非公開プロファイルのインプットの可視性を高めながら、メインプロファイルをすっきりと見やすくすることができます。 その場合は、非公開プロファイルが非公開であることを文書化する必要があります。
</p>

  <p> このコードが別のプロファイルによって使用される<em>可能性がある</em>場合は、ルール1を順守してください。
</p>
</blockquote>

<h3 id="diff-of-seventh-refactor">7回目のリファクタリングの差分</h3>

<pre><code class="language-diff">@@ -1,8 +1,9 @@
 # /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
 class profile::jenkins::master (
-  String                      $jenkins_port = '9091',
   Boolean                     $backups_enabled = false,
   Boolean                     $manage_plugins = false,
+  Boolean                     $ssl = false,
+  Optional[String[1]]         $site_alias = undef,
   Variant[String[1], Boolean] $direct_download = 'http://pkg.jenkins-ci.org/debian-stable/binary/jenkins_1.642.2_all.deb',
   Optional[String[1]]         $jenkins_logs_to_syslog = undef,
   Boolean                     $install_jenkins_java = true,
@@ -11,6 +12,9 @@ class profile::jenkins::master (
   # We rely on virtual resources that are ultimately declared by profile::server.
   include profile::server

+  # Deploy the SSL certificate/chain/key for sites on this domain.
+  include profile::ssl::delivery_wildcard
+
   # Some default values that vary by OS:
   include profile::jenkins::params
   $jenkins_owner          = $profile::jenkins::params::jenkins_owner
@@ -22,6 +26,31 @@ class profile::jenkins::master (
     include profile::jenkins::master::plugins
   }

+  motd::register { 'Jenkins CI master (profile::jenkins::master)': }
+
+  # This adds the site_alias to the message of the day for convenience when
+  # logging into a server via FQDN. Because of the way motd::register works, we
+  # need a sort of funny formatting to put it at the end (order =&gt; 25) and to
+  # list a class so there isn't a random "--" at the end of the message.
+  if $site_alias {
+    motd::register { 'jenkins-site-alias':
+      content =&gt; @("END"),
+                 profile::jenkins::master::proxy
+
+                 Jenkins site alias: ${site_alias}
+                 |-END
+      order   =&gt; 25,
+    }
+  }
+
+  # This is a "private" profile that sets up an Nginx proxy -- it's only ever
+  # declared in this class, and it would work identically pasted inline.
+  # But since it's long, this class reads more cleanly with it separated out.
+  class { 'profile::jenkins::master::proxy':
+    site_alias  =&gt; $site_alias,
+    require_ssl =&gt; $ssl,
+  }
+
   # Sensitive info (like SSH keys) isn't checked into version control like the
   # rest of our modules; instead, it's served from a custom mount point on a
   # designated server.
@@ -56,16 +85,11 @@ class profile::jenkins::master (
     version            =&gt; 'latest',
     service_enable     =&gt; true,
     service_ensure     =&gt; running,
-    configure_firewall =&gt; true,
+    configure_firewall =&gt; false,
     install_java       =&gt; $install_jenkins_java,
     manage_user        =&gt; false,
     manage_group       =&gt; false,
     manage_datadirs    =&gt; false,
-    port               =&gt; $jenkins_port,
-    config_hash        =&gt; {
-      'HTTP_PORT'    =&gt; { 'value' =&gt; $jenkins_port },
-      'JENKINS_PORT' =&gt; { 'value' =&gt; $jenkins_port },
-    },
   }

   # When not using the jenkins module's java version, install java8.
</code></pre>

<h2 id="the-final-code">ファイナルコード</h2>

<p> リファクタリング(および微調整)を行った後、<code>profile::jenkins::master</code>のファイナルコードは以下のとおりです。
</p>

<pre><code class="language-puppet"># /etc/puppetlabs/code/environments/production/site/profile/manifests/jenkins/master.pp
# Class: profile::jenkins::master
#
# Install a Jenkins master that meets Puppet's internal needs.
#
class profile::jenkins::master (
  Boolean                     $backups_enabled = false,
  Boolean                     $manage_plugins = false,
  Boolean                     $ssl = false,
  Optional[String[1]]         $site_alias = undef,
  Variant[String[1], Boolean] $direct_download = 'http://pkg.jenkins-ci.org/debian-stable/binary/jenkins_1.642.2_all.deb',
  Optional[String[1]]         $jenkins_logs_to_syslog = undef,
  Boolean                     $install_jenkins_java = true,
) {

  # We rely on virtual resources that are ultimately declared by profile::server.
  include profile::server

  # Deploy the SSL certificate/chain/key for sites on this domain.
  include profile::ssl::delivery_wildcard

  # Some default values that vary by OS:
  include profile::jenkins::params
  $jenkins_owner          = $profile::jenkins::params::jenkins_owner
  $jenkins_group          = $profile::jenkins::params::jenkins_group
  $master_config_dir      = $profile::jenkins::params::master_config_dir

  if $manage_plugins {
    # About 40 jenkins::plugin resources:
    include profile::jenkins::master::plugins
  }

  motd::register { 'Jenkins CI master (profile::jenkins::master)': }

  # This adds the site_alias to the message of the day for convenience when
  # logging into a server via FQDN. Because of the way motd::register works, we
  # need a sort of funny formatting to put it at the end (order =&gt; 25) and to
  # list a class so there isn't a random "--" at the end of the message.
  if $site_alias {
    motd::register { 'jenkins-site-alias':
      content =&gt; @("END"),
                 profile::jenkins::master::proxy

                 Jenkins site alias: ${site_alias}
                 |-END
      order   =&gt; 25,
    }
  }

  # This is a "private" profile that sets up an Nginx proxy -- it's only ever
  # declared in this class, and it would work identically pasted inline.
  # But since it's long, this class reads more cleanly with it separated out.
  class { 'profile::jenkins::master::proxy':
    site_alias  =&gt; $site_alias,
    require_ssl =&gt; $ssl,
  }

  # Sensitive info (like SSH keys) isn't checked into version control like the
  # rest of our modules; instead, it's served from a custom mount point on a
  # designated server.
  $secure_server = lookup('puppetlabs::ssl::secure_server')

  # Dependencies:
  #   - Pull in apt if we're on Debian.
  #   - Pull in the 'git' package, used by Jenkins for Git polling.
  #   - Manage the 'run' directory (fix for busted Jenkins packaging).
  if $::osfamily == 'Debian' { include apt }

  package { 'git':
    ensure =&gt; present,
  }

  file { '/var/run/jenkins': ensure =&gt; 'directory' }

  # Because our account::user class manages the '${master_config_dir}' directory
  # as the 'jenkins' user's homedir (as it should), we need to manage
  # `${master_config_dir}/plugins` here to prevent the upstream
  # rtyler-jenkins module from trying to manage the homedir as the config
  # dir. For more info, see the upstream module's `manifests/plugin.pp`
  # manifest.
  file { "${master_config_dir}/plugins":
    ensure  =&gt; directory,
    owner   =&gt; $jenkins_owner,
    group   =&gt; $jenkins_group,
    mode    =&gt; '0755',
    require =&gt; [Group[$jenkins_group], User[$jenkins_owner]],
  }

  Account::User &lt;| tag == 'jenkins' |&gt;

  class { 'jenkins':
    lts                =&gt; true,
    repo               =&gt; true,
    direct_download    =&gt; $direct_download,
    version            =&gt; 'latest',
    service_enable     =&gt; true,
    service_ensure     =&gt; running,
    configure_firewall =&gt; false,
    install_java       =&gt; $install_jenkins_java,
    manage_user        =&gt; false,
    manage_group       =&gt; false,
    manage_datadirs    =&gt; false,
  }

  # When not using the jenkins module's java version, install java8.
  unless $install_jenkins_java  { include profile::jenkins::usage::java8 }

  # Manage the heap size on the master, in MB.
  if($::memorysize_mb =~ Number and $::memorysize_mb &gt; 8192)
  {
    # anything over 8GB we should keep max 4GB for OS and others
    $heap = sprintf('%.0f', $::memorysize_mb - 4096)
  } else {
    # This is calculated as 50% of the total memory.
    $heap = sprintf('%.0f', $::memorysize_mb * 0.5)
  }
  # Set java params, like heap min and max sizes. See
  # https://wiki.jenkins-ci.org/display/JENKINS/Features+controlled+by+system+properties
  jenkins::sysconfig { 'JAVA_ARGS':
    value =&gt; "-Xms${heap}m -Xmx${heap}m -Djava.awt.headless=true -XX:+UseConcMarkSweepGC -XX:+CMSClassUnloadingEnabled -Dhudson.model.DirectoryBrowserSupport.CSP=\\\"default-src 'self'; img-src 'self'; style-src 'self';\\\"",
  }

  # Forward jenkins master logs to syslog.
  # When set to facility.level the jenkins_log will use that value instead of a
  # separate log file eg. daemon.info
  if $jenkins_logs_to_syslog {
    jenkins::sysconfig { 'JENKINS_LOG':
      value =&gt; "$jenkins_logs_to_syslog",
    }
  }

  # Deploy the SSH keys that Jenkins needs to manage its agent machines and
  # access Git repos.
  file { "${master_config_dir}/.ssh":
    ensure =&gt; directory,
    owner  =&gt; $jenkins_owner,
    group  =&gt; $jenkins_group,
    mode   =&gt; '0700',
  }

  file { "${master_config_dir}/.ssh/id_rsa":
    ensure =&gt; file,
    owner  =&gt; $jenkins_owner,
    group  =&gt; $jenkins_group,
    mode   =&gt; '0600',
    source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins",
  }

  file { "${master_config_dir}/.ssh/id_rsa.pub":
    ensure =&gt; file,
    owner  =&gt; $jenkins_owner,
    group  =&gt; $jenkins_group,
    mode   =&gt; '0640',
    source =&gt; "puppet://${secure_server}/secure/delivery/id_rsa-jenkins.pub",
  }

  # Back up Jenkins' data.
  if $backups_enabled {
    backup::job { "jenkins-data-${::hostname}":
      files =&gt; $master_config_dir,
    }
  }

  # (QENG-1829) Logrotate rules:
  # Jenkins' default logrotate config retains too much data: by default, it
  # rotates jenkins.log weekly and retains the last 52 weeks of logs.
  # Considering we almost never look at the logs, let's rotate them daily
  # and discard after 7 days to reduce disk usage.
  logrotate::job { 'jenkins':
    log     =&gt; '/var/log/jenkins/jenkins.log',
    options =&gt; [
      'daily',
      'copytruncate',
      'missingok',
      'rotate 7',
      'compress',
      'delaycompress',
      'notifempty'
    ],
  }

}
</code></pre>
</div>
